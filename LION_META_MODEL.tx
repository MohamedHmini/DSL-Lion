MainRule:
    rules*=GeneralRule
;

GeneralRule:
    (Load | Save | Drop | Modify | Rename | Set | Impute | Map | Encode) ';'
;

FILEPATH:
    /(\/)?([a-zA-Z0-1_.]+(\/)?)*[a-zA-Z0-9_.]+/ 
;

Encapsulated_FILEPATH:
    ('"' file_path=FILEPATH '"' | "'" file_path=FILEPATH "'")
;

FeatureVal:
    val=ID | val=STRING
;

Feature:
    type=Dtype v_name=FeatureVal ','
;


Load:
    'FROM' Encapsulated_FILEPATH 'LOAD' (('ONLY')? '(' 
        feature+=Feature
    ')')? 'AS' name=ID ('SPLIT' 'USING' sep=STRING)?
;

Save:
    'SAVE' ('LIONFRAME' lionFrame=[Load] | 'CODE') 'AS' Encapsulated_FILEPATH
;


Alter:
    'ALTER' 'LIONFRAME' lionFrame=[Load]
;

Drop:
    Alter 'DROP' ('FEATURE' features=FeatureVal (',' features+=FeatureVal)? | Dropna)
;


Dropna:
    'NAN' ('FEATURE' | 'ROW') (Dropna_subset | Dropna_thresh)?
;

Dropna_subset:
    'ON' 'SUBSET' '(' features=FeatureVal (',' features+=FeatureVal)* ')'
;

Dropna_thresh:
    'WITH' ('FEWER' 'THAN' thresh=INT | ('ALL' | 'ANY') 'NAN')
;


Column:
    v_name=FeatureVal type=Dtype
;

Modify:
    Alter 'MODIFY' 'FEATURE' columns=Column (',' columns+=Column)*
;

RenameTo:
    old_feature=FeatureVal 'TO' new_feature=FeatureVal
;

Rename:
    Alter 'RENAME' 'FEATURE' renames=RenameTo (',' renames+=RenameTo)*
;

Assign:
    feature=FeatureVal '=' val=GeneralType
;

Update:
    'UPDATE' lionFrame=[Load]
;

Set:
    Update 'SET' assigns=Assign (',' assigns+=Assign)* (Where)?
;

Cmp:
    feature=FeatureVal op=Operator val=GeneralType
;

Where:
    'WHERE' cmps=Cmp (logical_op=LogicalOperator cmps+=Cmp)*
;

Impute:
    Update 'IMPUTE' assigns=Assign (',' assigns+=Assign)* 'USING' stragety=ImputationStrategy
;

Map:
    Update 'MAP' mapcolumns=MapColumn (',' mapcolumns+=MapColumn)*
;

MapColumn:
    feature=FeatureVal 'USING' MapDict
;

MapDict:
    '(' categories=MapTo (',' categories+=MapTo)* ')'
;

MapTo:
    category=STRING ':' val=INT
;

Encode:
    Update 'ENCODE' features=FeatureVal (',' features+=FeatureVal)*
;

GeneralType:
    STRING | FLOAT | INT | 'NaN'
;

Operator:
    '==' | '<=' | '>=' | '<' | '>' | '!='
;

LogicalOperator:
    'AND' | 'OR'
;

Dtype:
    'int8' | 'int16' | 'int32' | 'int64' | 'uint8' | 'uint16' | 'uint32' | 'uint64' | 'float16' | 'float32' | 'float64' | 'complex64' | 'complex128' | 'bool' | 'object' | 'bytes' | 'str' | 'void'
;


ImputationStrategy:
    'MEAN' | 'MEDIAN' | 'MOST_FREQUENT'
;

Algo:
    'CHI2' | 'IMPUTATION' | 'Z-SCORE' | 'MIN-MAX-SCORE'
;


Comment:
  /#.*$/
;



